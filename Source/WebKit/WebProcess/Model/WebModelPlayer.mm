/*
 * Copyright (C) 2021-2023 Apple Inc. All rights reserved.
 * Copyright (C) 2025 Samuel Weinig <sam@webkit.org>
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions
 * are met:
 * 1. Redistributions of source code must retain the above copyright
 *    notice, this list of conditions and the following disclaimer.
 * 2. Redistributions in binary form must reproduce the above copyright
 *    notice, this list of conditions and the following disclaimer in the
 *    documentation and/or other materials provided with the distribution.
 *
 * THIS SOFTWARE IS PROVIDED BY APPLE INC. ``AS IS'' AND ANY
 * EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR
 * PURPOSE ARE DISCLAIMED.  IN NO EVENT SHALL APPLE INC. OR
 * CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL,
 * EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO,
 * PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR
 * PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY
 * OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 * (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
 * OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

#import "config.h"
#import "WebModelPlayer.h"

#if ENABLE(GPU_PROCESS_MODEL)

#import "Mesh.h"
#import "ModelInlineConverters.h"
#import "ModelTypes.h"
#import "RemoteGPUProxy.h"
#import "WKStageModeOrbitSimulator.h"
#import "WebKitSwiftSoftLink.h"
#import <WebCore/Document.h>
#import <WebCore/FloatPoint3D.h>
#import <WebCore/GPU.h>
#import <WebCore/GraphicsLayer.h>
#import <WebCore/GraphicsLayerContentsDisplayDelegate.h>
#import <WebCore/HTMLModelElement.h>
#import <WebCore/ModelPlayerGraphicsLayerConfiguration.h>
#import <WebCore/Navigator.h>
#import <WebCore/Page.h>
#import <WebCore/PlatformCALayer.h>
#import <WebCore/PlatformCALayerDelegatedContents.h>
#import <wtf/RetainPtr.h>

#if PLATFORM(COCOA)
#import <Metal/Metal.h>
#endif

namespace WebKit {

class ModelDisplayBufferDisplayDelegate final : public WebCore::GraphicsLayerContentsDisplayDelegate {
public:
    static Ref<ModelDisplayBufferDisplayDelegate> create(WebModelPlayer& modelPlayer, bool isOpaque = false, float contentsScale = 1)
    {
        return adoptRef(*new ModelDisplayBufferDisplayDelegate(modelPlayer, isOpaque, contentsScale));
    }
    // GraphicsLayerContentsDisplayDelegate overrides.
    void prepareToDelegateDisplay(WebCore::PlatformCALayer& layer) final
    {
        layer.setOpaque(m_isOpaque);
        layer.setContentsScale(m_contentsScale);
        layer.setContentsFormat(m_contentsFormat);
    }
    void display(WebCore::PlatformCALayer& layer) final
    {
        if (layer.isOpaque() != m_isOpaque)
            layer.setOpaque(m_isOpaque);
        if (m_displayBuffer) {
            layer.setContentsFormat(m_contentsFormat);
            layer.setDelegatedContents({ MachSendRight { m_displayBuffer }, { }, std::nullopt });
        } else
            layer.clearContents();

        layer.setNeedsDisplay();

        if (RefPtr player = m_modelPlayer.get())
            player->update();

    }
    WebCore::GraphicsLayer::CompositingCoordinatesOrientation orientation() const final
    {
        return WebCore::GraphicsLayer::CompositingCoordinatesOrientation::TopDown;
    }
    void setDisplayBuffer(const WTF::MachSendRight& displayBuffer)
    {
        if (!displayBuffer) {
            m_displayBuffer = { };
            return;
        }

        if (m_displayBuffer && displayBuffer.sendRight() == m_displayBuffer.sendRight())
            return;

        m_displayBuffer = MachSendRight { displayBuffer };
    }
    void setContentsFormat(WebCore::ContentsFormat contentsFormat)
    {
        m_contentsFormat = contentsFormat;
    }
    void setOpaque(bool opaque)
    {
        m_isOpaque = opaque;
    }
private:
    ModelDisplayBufferDisplayDelegate(WebModelPlayer& modelPlayer, bool isOpaque, float contentsScale)
        : m_modelPlayer(modelPlayer)
        , m_contentsScale(contentsScale)
        , m_isOpaque(isOpaque)
    {
    }
    ThreadSafeWeakPtr<WebModelPlayer> m_modelPlayer;
    WTF::MachSendRight m_displayBuffer;
    const float m_contentsScale;
    bool m_isOpaque;
#if ENABLE(PIXEL_FORMAT_RGBA16F)
    WebCore::ContentsFormat m_contentsFormat { WebCore::ContentsFormat::RGBA16F };
#else
    WebCore::ContentsFormat m_contentsFormat { WebCore::ContentsFormat::RGBA8 };
#endif
};

Ref<WebModelPlayer> WebModelPlayer::create(WebCore::Page& page, WebCore::ModelPlayerClient& client)
{
    return adoptRef(*new WebModelPlayer(page, client));
}

WebModelPlayer::WebModelPlayer(WebCore::Page& page, WebCore::ModelPlayerClient& client)
: m_client { client }
, m_id { WebCore::ModelPlayerIdentifier::generate() }
, m_page(page)
{
}

WebModelPlayer::~WebModelPlayer()
{
}

void WebModelPlayer::ensureOnMainThreadWithProtectedThis(Function<void(Ref<WebModelPlayer>)>&& task)
{
    ensureOnMainThread([protectedThis = Ref { *this }, task = WTF::move(task)]() mutable {
        task(protectedThis);
    });
}

double WebModelPlayer::duration() const
{
    return [m_modelLoader duration];
}

static Vector<uint8_t> loadData(RetainPtr<CFStringRef> filename)
{
    RetainPtr<NSBundle> myBundle = [NSBundle bundleWithIdentifier:@"com.apple.WebCore"];
    RetainPtr<NSURL> nsFileURL = [myBundle URLForResource:(__bridge NSString *)filename.get() withExtension:@""];
    RetainPtr<NSData> data = [NSData dataWithContentsOfURL:nsFileURL.get() options:0 error:nil];
    return makeVector(data.get());
}

static MTLPixelFormat computePixelFormat(size_t bytesPerComponent, size_t channelCount)
{
    switch (bytesPerComponent) {
    default:
    case 1:
        switch (channelCount) {
        case 1:
            return MTLPixelFormatR8Unorm;
        case 2:
            return MTLPixelFormatRG8Unorm;
        case 4:
        default:
            return MTLPixelFormatRGBA8Unorm;
        }
    case 2:
        switch (channelCount) {
        case 1:
            return MTLPixelFormatR16Float;
        case 2:
            return MTLPixelFormatRG16Float;
        case 4:
        default:
            return MTLPixelFormatRGBA16Float;
        }
    case 4:
        switch (channelCount) {
        case 1:
            return MTLPixelFormatR32Float;
        case 2:
            return MTLPixelFormatRG32Float;
        case 4:
        default:
            return MTLPixelFormatRGBA32Float;
        }
    }
}

static std::optional<WebModel::ImageAsset> loadIBL(Ref<WebCore::SharedBuffer>&& data)
{
    RetainPtr imageAssetData = data->createNSData();
    RetainPtr imageSource = adoptCF(CGImageSourceCreateWithData((CFDataRef)imageAssetData.get(), nullptr));
    if (!imageSource) {
        ASSERT_NOT_REACHED();
        return std::nullopt;
    }

    RetainPtr platformImage = adoptCF(CGImageSourceCreateImageAtIndex(imageSource.get(), 0, nullptr));
    if (!platformImage) {
        ASSERT_NOT_REACHED();
        return std::nullopt;
    }

    RetainPtr pixelDataCfData = adoptCF(CGDataProviderCopyData(CGImageGetDataProvider(platformImage.get())));
    auto byteSpan = span(pixelDataCfData.get());

    auto width = CGImageGetWidth(platformImage.get());
    auto height = CGImageGetHeight(platformImage.get());
    auto bytesPerPixel = static_cast<size_t>(byteSpan.size() / (width * height));
    auto bytesPerComponent = CGImageGetBitsPerComponent(platformImage.get()) / 8;

    MTLPixelFormat pixelFormat = computePixelFormat(bytesPerComponent, bytesPerPixel / bytesPerComponent);

    return WebModel::ImageAsset {
        .data = Vector<uint8_t> { byteSpan },
        .width = static_cast<long>(width),
        .height = static_cast<long>(height),
        .depth = 1,
        .bytesPerPixel = static_cast<long>(bytesPerPixel),
        .textureType = WebCore::WebGPU::TextureViewDimension::_2d,
        .pixelFormat = toTextureFormat(pixelFormat),
        .mipmapLevelCount = 1,
        .arrayLength = 1,
        .textureUsage = WebCore::WebGPU::TextureUsage::TextureBinding,
        .swizzle = WebModel::ImageAssetSwizzle {
            .red = MTLTextureSwizzleRed,
            .green = MTLTextureSwizzleGreen,
            .blue = MTLTextureSwizzleBlue,
            .alpha = MTLTextureSwizzleAlpha
        }
    };
}

// MARK: - ModelPlayer overrides.

void WebModelPlayer::load(WebCore::Model& modelSource, WebCore::LayoutSize size)
{
    RefPtr corePage = m_page.get();
    m_modelLoader = nil;
    m_didFinishLoading = false;
    RefPtr document = corePage->localTopDocument();
    if (!document)
        return;

    RefPtr window = document->window();
    if (!window)
        return;

    RefPtr gpu = protect(window->navigator())->gpu();
    if (!gpu)
        return;

    size.scale(document->deviceScaleFactor());
    m_currentPixelSize = WebCore::IntSize(size.width().toUnsigned(), size.height().toUnsigned());

    WebModel::ImageAsset diffuseTexture {
        .data = loadData(adoptCF(static_cast<CFStringRef>(@"modelDefaultDiffuseData"))),
        .width = 64,
        .height = 64,
        .depth = 1,
        .bytesPerPixel = 2,
        .textureType = WebCore::WebGPU::TextureViewDimension::Cube,
        .pixelFormat = WebCore::WebGPU::TextureFormat::R16float,
        .mipmapLevelCount = 0,
        .arrayLength = 6,
        .textureUsage = WebCore::WebGPU::TextureUsage::TextureBinding,
        .swizzle = { }
    };
    WebModel::ImageAsset specularTexture {
        .data = loadData(adoptCF(static_cast<CFStringRef>(@"modelDefaultSpecularData"))),
        .width = 256,
        .height = 256,
        .depth = 1,
        .bytesPerPixel = 2,
        .textureType = WebCore::WebGPU::TextureViewDimension::Cube,
        .pixelFormat = WebCore::WebGPU::TextureFormat::R16float,
        .mipmapLevelCount = 0,
        .arrayLength = 6,
        .textureUsage = WebCore::WebGPU::TextureUsage::TextureBinding,
        .swizzle = { }
    };

    m_currentModel = static_cast<RemoteGPUProxy&>(gpu->backing()).createModelBacking(m_currentPixelSize.width(), m_currentPixelSize.height(), diffuseTexture, specularTexture, [protectedThis = Ref { *this }] (Vector<MachSendRight>&& surfaceHandles) {
        if (surfaceHandles.size())
            protectedThis->m_displayBuffers = WTF::move(surfaceHandles);
    });
    m_currentModel->setViewportSize(size.width().toFloat(), size.height().toFloat());

    m_modelLoader = adoptNS([allocWKBridgeModelLoaderInstance() init]);
    Ref protectedThis = Ref { *this };
    [m_modelLoader setCallbacksWithModelUpdatedCallback:^(WKBridgeUpdateMesh *updateRequest) {
        ensureOnMainThreadWithProtectedThis([updateRequest] (Ref<WebModelPlayer> protectedThis) {
            RefPtr model = protectedThis->m_currentModel;
            if (model) {
                model->update(toCpp(updateRequest));
                protectedThis->setStageMode(protectedThis->m_stageMode);
            }

            [protectedThis->m_modelLoader requestCompleted:updateRequest];

            if (RefPtr client = protectedThis->m_client.get(); client && !protectedThis->m_didFinishLoading) {
                protectedThis->m_didFinishLoading = true;
                [protectedThis->m_modelLoader setLoop:protectedThis->m_isLooping];

                client->didFinishLoading(protectedThis.get());
                auto [simdCenter, simdExtents] = model->getCenterAndExtents();
                client->didUpdateBoundingBox(protectedThis.get(), WebCore::FloatPoint3D(simdCenter.x, simdCenter.y, simdCenter.z), WebCore::FloatPoint3D(simdExtents.x, simdExtents.y, simdExtents.z));
                protectedThis->notifyEntityTransformUpdated();

                auto environmentMap = protectedThis->m_environmentMap;
                if (model && environmentMap) {
                    if (auto environmentMapImage = loadIBL(WTF::move(*environmentMap))) {
                        model->setEnvironmentMap(*environmentMapImage);
                        protectedThis->m_environmentMap = std::nullopt;
                    }
                }
            }
        });
    } textureUpdatedCallback:^(WKBridgeUpdateTexture *updateTexture) {
        ensureOnMainThreadWithProtectedThis([updateTexture] (Ref<WebModelPlayer> protectedThis) {
            if (protectedThis->m_currentModel)
                protectedThis->m_currentModel->updateTexture(toCpp(updateTexture));

            [protectedThis->m_modelLoader requestCompleted:updateTexture];
        });
    } materialUpdatedCallback:^(WKBridgeUpdateMaterial *updateMaterial) {
        ensureOnMainThreadWithProtectedThis([updateMaterial] (Ref<WebModelPlayer> protectedThis) {
            if (protectedThis->m_currentModel)
                protectedThis->m_currentModel->updateMaterial(toCpp(updateMaterial));

            [protectedThis->m_modelLoader requestCompleted:updateMaterial];
        });
    }];

    m_retainedData = modelSource.data()->createNSData();
    [m_modelLoader loadModel:m_retainedData.get()];
}

void WebModelPlayer::notifyEntityTransformUpdated()
{
    RefPtr model = m_currentModel;
    RefPtr client = m_client.get();
    if (!model || !client || !model->entityTransform())
        return;

    auto scaledTransform = *model->entityTransform();
    auto scale = m_currentScale;
    scaledTransform.column0 *= scale;
    scaledTransform.column1 *= scale;
    scaledTransform.column2 *= scale;
    client->didUpdateEntityTransform(*this, WebCore::TransformationMatrix(static_cast<simd_float4x4>(scaledTransform)));
}

void WebModelPlayer::sizeDidChange(WebCore::LayoutSize size)
{
    RefPtr currentModel = m_currentModel;
    if (!currentModel)
        return;

    RefPtr corePage = m_page.get();
    if (!corePage)
        return;
    RefPtr document = corePage->localTopDocument();
    if (!document)
        return;

    size.scale(document->deviceScaleFactor());
    auto newPixelSize = WebCore::IntSize(size.width().toUnsigned(), size.height().toUnsigned());
    if (newPixelSize == m_currentPixelSize)
        return;

    m_currentPixelSize = newPixelSize;

    currentModel->sizeDidChange(newPixelSize.width(), newPixelSize.height(), [protectedThis = Ref { *this }](Vector<MachSendRight>&& newBuffers) {
        if (newBuffers.isEmpty())
            return;

        protectedThis->m_displayBuffers = WTF::move(newBuffers);
        protectedThis->m_currentTexture = 0;
        if (protectedThis->m_contentsDisplayDelegate)
            RefPtr { protectedThis->m_contentsDisplayDelegate }->setDisplayBuffer(*protectedThis->displayBuffer());
    });

    m_currentScale = static_cast<float>(size.minDimension());
    if (RefPtr model = m_currentModel)
        model->setViewportSize(size.width().toFloat(), size.height().toFloat());
    notifyEntityTransformUpdated();
}

void WebModelPlayer::enterFullscreen()
{
}

void WebModelPlayer::handleMouseDown(const WebCore::LayoutPoint& startingPoint, MonotonicTime)
{
    m_initialPoint = startingPoint;
    if (!m_orbitSimulator)
        m_orbitSimulator = adoptNS([[WKStageModeOrbitSimulator alloc] init]);
    [m_orbitSimulator gestureDidBegin];
}

void WebModelPlayer::handleMouseMove(const WebCore::LayoutPoint& currentPoint, MonotonicTime)
{
    if (!m_initialPoint)
        return;

    static constexpr float kDragToRotationMultiplier = 0.005;

    float totalDeltaX = static_cast<float>(m_initialPoint->x() - currentPoint.x()) * kDragToRotationMultiplier;
    float totalDeltaY = static_cast<float>(currentPoint.y() - m_initialPoint->y()) * kDragToRotationMultiplier;

    RetainPtr orbitSimulator = m_orbitSimulator;
    if (!orbitSimulator)
        return;

    [orbitSimulator gestureDidUpdateWithDeltaX:totalDeltaX deltaY:totalDeltaY];
    if (RefPtr model = m_currentModel)
        model->setRotation([orbitSimulator currentYaw], [orbitSimulator currentPitch]);
}

bool WebModelPlayer::supportsMouseInteraction()
{
    return true;
}

void WebModelPlayer::handleMouseUp(const WebCore::LayoutPoint&, MonotonicTime)
{
    m_initialPoint = std::nullopt;
    if (RetainPtr orbitSimulator = m_orbitSimulator)
        [orbitSimulator gestureDidEnd];
}

void WebModelPlayer::getCamera(CompletionHandler<void(std::optional<WebCore::HTMLModelElementCamera>&&)>&&)
{
}

void WebModelPlayer::setCamera(WebCore::HTMLModelElementCamera, CompletionHandler<void(bool success)>&&)
{
}

void WebModelPlayer::isPlayingAnimation(CompletionHandler<void(std::optional<bool>&&)>&&)
{
}

void WebModelPlayer::setAnimationIsPlaying(bool, CompletionHandler<void(bool success)>&&)
{
}

void WebModelPlayer::isLoopingAnimation(CompletionHandler<void(std::optional<bool>&&)>&& completion)
{
    completion(m_isLooping);
}

void WebModelPlayer::setIsLoopingAnimation(bool shouldLoop, CompletionHandler<void(bool success)>&& completion)
{
    m_isLooping = shouldLoop;
    completion(shouldLoop);
}

void WebModelPlayer::animationDuration(CompletionHandler<void(std::optional<Seconds>&&)>&& completion)
{
    completion(Seconds(duration()));
}

void WebModelPlayer::animationCurrentTime(CompletionHandler<void(std::optional<Seconds>&&)>&&)
{
}

void WebModelPlayer::setAnimationCurrentTime(Seconds, CompletionHandler<void(bool success)>&&)
{
}

void WebModelPlayer::hasAudio(CompletionHandler<void(std::optional<bool>&&)>&&)
{
}

void WebModelPlayer::isMuted(CompletionHandler<void(std::optional<bool>&&)>&&)
{
}

void WebModelPlayer::setIsMuted(bool, CompletionHandler<void(bool success)>&&)
{
}

void WebModelPlayer::updateScene()
{
}

WebCore::ModelPlayerAccessibilityChildren WebModelPlayer::accessibilityChildren()
{
    return { };
}

WebCore::ModelPlayerIdentifier WebModelPlayer::identifier() const
{
    return m_id;
}

void WebModelPlayer::configureGraphicsLayer(WebCore::GraphicsLayer& graphicsLayer, WebCore::ModelPlayerGraphicsLayerConfiguration&& configuration)
{
    graphicsLayer.setContentsDisplayDelegate(contentsDisplayDelegate(), WebCore::GraphicsLayer::ContentsLayerPurpose::Canvas);
    if (RefPtr currentModel = m_currentModel) {
        auto backgroundColor = configuration.backgroundColor;
        if (backgroundColor.isValid()) {
            auto opaqueColor = backgroundColor.opaqueColor();
            auto [r, g, b, _a] = opaqueColor.toResolvedColorComponentsInColorSpace(WebCore::ColorSpace::LinearSRGB);
            currentModel->setBackgroundColor(simd_make_float3(r, g, b));
        }
    }
}

const MachSendRight* WebModelPlayer::displayBuffer() const
{
    if (m_currentTexture >= m_displayBuffers.size())
        return nullptr;

    return &m_displayBuffers[m_currentTexture];
}

WebCore::GraphicsLayerContentsDisplayDelegate* WebModelPlayer::contentsDisplayDelegate()
{
    if (!m_contentsDisplayDelegate) {
        RefPtr modelDisplayDelegate = ModelDisplayBufferDisplayDelegate::create(*this);
        m_contentsDisplayDelegate = modelDisplayDelegate;
        modelDisplayDelegate->setDisplayBuffer(*displayBuffer());
    }

    return m_contentsDisplayDelegate.get();
}

void WebModelPlayer::simulate(float elapsedTime)
{
    RefPtr model = m_currentModel;
    if (!model || !m_didFinishLoading)
        return;

    RetainPtr orbitSimulator = m_orbitSimulator;
    if (!orbitSimulator)
        return;
    if ([orbitSimulator stepWithElapsedTime:elapsedTime])
        model->setRotation([orbitSimulator currentYaw], [orbitSimulator currentPitch]);
}

void WebModelPlayer::setPlaybackRate(double newRate, CompletionHandler<void(double effectivePlaybackRate)>&& completion)
{
    m_playbackRate = newRate;
    completion(newRate);
}

void WebModelPlayer::update()
{
    auto now = MonotonicTime::now();
    float elapsed = m_lastUpdateTime ? static_cast<float>((now - m_lastUpdateTime).seconds()) : (1.f / 60.f);
    float elapsedTime = std::clamp(elapsed, 1.f / 120.f, 1.f / 15.f);
    m_lastUpdateTime = now;

    simulate(elapsedTime);

    auto timeDelta = paused() ? 0.f : (m_playbackRate * elapsedTime);

    [m_modelLoader update:timeDelta];

    if (!m_isLooping && !paused() && [m_modelLoader currentTime] >= [m_modelLoader duration])
        m_pauseState = PauseState::Paused;

    if (m_didFinishLoading) {
        if (RefPtr currentModel = m_currentModel)
            currentModel->render();

        if (++m_currentTexture >= m_displayBuffers.size())
            m_currentTexture = 0;
        if (auto* machSendRight = displayBuffer(); machSendRight && contentsDisplayDelegate())
            RefPtr { m_contentsDisplayDelegate }->setDisplayBuffer(*machSendRight);
    }

    if (RefPtr client = m_client.get())
        client->didUpdate(*this);
}

bool WebModelPlayer::supportsTransform(WebCore::TransformationMatrix transformationMatrix)
{
    if (m_stageMode != WebCore::StageModeOperation::None)
        return false;

    if (RefPtr currentModel = m_currentModel)
        return currentModel->supportsTransform(transformationMatrix);

    return false;
}

void WebModelPlayer::play(bool playing)
{
    if (RefPtr model = m_currentModel) {
        if (playing && !m_isLooping && [m_modelLoader currentTime] >= [m_modelLoader duration])
            [m_modelLoader setCurrentTime:0];
        model->play(playing);
        m_pauseState = playing ? PauseState::Playing : PauseState::Paused;
    }
}

void WebModelPlayer::setLoop(bool loop)
{
    if (m_isLooping == loop)
        return;

    m_isLooping = loop;
    [m_modelLoader setLoop:loop];
}

void WebModelPlayer::setAutoplay(bool autoplay)
{
    if (m_pauseState == PauseState::Paused)
        return;

    play(autoplay);
    m_pauseState = autoplay ? PauseState::Playing : PauseState::Paused;
}

void WebModelPlayer::setPaused(bool paused, CompletionHandler<void(bool succeeded)>&& completion)
{
    play(!paused);
    completion(!!m_currentModel);
}

bool WebModelPlayer::paused() const
{
    return m_pauseState != PauseState::Playing;
}

Seconds WebModelPlayer::currentTime() const
{
    return Seconds([m_modelLoader currentTime]);
}

void WebModelPlayer::setCurrentTime(Seconds currentTime, CompletionHandler<void()>&& completion)
{
    double clamped = std::clamp(currentTime.seconds(), 0.0, duration());
    [m_modelLoader setCurrentTime:clamped];
    completion();
}

std::optional<WebCore::TransformationMatrix> WebModelPlayer::entityTransform() const
{
#if PLATFORM(COCOA)
    if (RefPtr model = m_currentModel) {
        if (auto transform = model->entityTransform())
            return static_cast<simd_float4x4>(*transform);
    }
#endif
    return std::nullopt;
}

void WebModelPlayer::setStageMode(WebCore::StageModeOperation stageMode)
{
    m_stageMode = stageMode;
    if (RefPtr model = m_currentModel) {
        model->setStageMode(m_stageMode);
        notifyEntityTransformUpdated();
    }
}

void WebModelPlayer::setEntityTransform(WebCore::TransformationMatrix matrix)
{
    if (RefPtr model = m_currentModel) {
        model->setEntityTransform(static_cast<simd_float4x4>(matrix));
        notifyEntityTransformUpdated();
    }
}

void WebModelPlayer::setEnvironmentMap(Ref<WebCore::SharedBuffer>&& data)
{
    bool success = false;
    if (RefPtr currentModel = m_currentModel; currentModel && m_didFinishLoading) {
        if (auto environmentMap = loadIBL(WTF::move(data))) {
            currentModel->setEnvironmentMap(*environmentMap);
            m_environmentMap = std::nullopt;
        }
        success = true;
    } else
        m_environmentMap = WTF::move(data);

    if (RefPtr client = m_client.get())
        client->didFinishEnvironmentMapLoading(*this, success);
}

}

#endif
